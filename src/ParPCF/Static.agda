module ParPCF.Static where

infixl 10 _,_
infix   9 _∋_ _⊢_

data Ty : Set where
  `N : Ty
  _`→_ _`×_ : Ty -> Ty -> Ty

data Ctx : Set where
  ∅ : Ctx
  _,_ : Ctx -> Ty -> Ctx

data _∋_ : Ctx -> Ty -> Set where
  H : ∀ {Γ τ} -> Γ , τ ∋ τ 
  T : ∀ {Γ τ σ} -> Γ ∋ τ -> Γ , σ ∋ τ
  
data _⊢_ : Ctx -> Ty -> Set where

  `Z : ∀ {Γ}
    ---------
    -> Γ ⊢ `N

  `S : ∀ {Γ}
    -> Γ ⊢ `N
    ---------
    -> Γ ⊢ `N

  `V : ∀ {Γ τ}
    -> Γ ∋ τ
    --------
    -> Γ ⊢ τ

  `let_`in_ :
    ∀ {Γ τ σ}
    -> Γ ⊢ σ
    -> Γ , σ ⊢ τ
    ------------
    -> Γ ⊢ τ
  
  `λ :
    ∀ {Γ τ σ}
    -> Γ , σ ⊢ τ
    --------------
    -> Γ ⊢ σ `→ τ

  _`$_ :
    ∀ {Γ τ σ}
    -> Γ ⊢ σ `→ τ
    -> Γ ⊢ σ
    --------
    -> Γ ⊢ τ
  
  _`,_ :
    ∀ {Γ τ σ}
    -> Γ ⊢ τ
    -> Γ ⊢ σ
    -------------
    -> Γ ⊢ τ `× σ

  `π₀ :
    ∀ {Γ τ σ}
    -> Γ ⊢ τ `× σ
    ---------------
    -> Γ ⊢ τ

  `π₁ :
    ∀ {Γ τ σ}
    -> Γ ⊢ τ `× σ
    --------------
    -> Γ ⊢ σ

  `join⟨_,_⟩ :
    ∀ {Γ τ σ}
    -> Γ ⊢ τ
    -> Γ ⊢ σ
    --------------
    -> Γ ⊢ τ `× σ 
